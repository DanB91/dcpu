; void* memset (void* ptr, int value, size_t num);
;
; Sets the first num words of the block of memory pointed by ptr to the
; specified value. This performs an overflow check on ptr+num and ensures
; we do not go beyond memory bounds.
;
; It returns the original ptr address in A.
;
;
; ## Version History:
;   0.1.1: Return original pointer value.
;   0.1.0: Initial implementation for spec 1.7.
;
:memset
  ife 0, c  ; zero size - exit.
    set pc, pop

  set push, a ; Save original address.

  sub c, 1
  add c, a  ; Set c to end address (ptr + num).
  ife 1, ex ; Check for overflow.
     set c, 0xffff

:memset_loop
  set [a], b
  ife a, c
    set pc, memset_end
  add a, 1
  set pc, memset_loop

:memset_end
  set a, pop ; Restore original address.
  set pc, pop

