; char* strpack( char* str )
;
; This function turns the given unpacked string into a packed version.
;
; Returns address of packed string.
; Retuns NULL if anything goes wrong or input string is already packed.
; The returned string is malloc'd and should therefor be free'd by the caller.
;
; ## Dependencies
;   malloc.dasm
;   strlen.dasm
;
; ## Version History:
;   0.1.0: Initial implementation for spec 1.7.
;
:strpack
  ; No need to continue with empty string.
  ifn [a], 0
    ifl [a], 0x100
      set pc, strpack_getlen

  set a, 0
  set pc, pop

:strpack_getlen
  set c, a

  ; Get length of input string.
  ; Length of packed string is half of this.
  jsr strlen

  add a, 1
  shr a, 1

  ; Allocate new chunk of memory for packed string.
  jsr malloc
  ife a, 0
    set pc, pop

  set push, x
  set x, a

:strpack_loop
  ife [c], 0
    set pc, strpack_ret

  set [a], [c]
  shl [a], 8
  add c, 1

  ife [c], 0
    set pc, strpack_ret

  add [i], [c]
  add a, 1
  set pc, strpack_loop

:strpack_ret
  set a, x
  set x, pop
  set pc, pop

